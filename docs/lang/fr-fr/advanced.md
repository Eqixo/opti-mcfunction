[‚Üê Retour au README](../../../README.md)

#### üåê Langue
[[EN]](../../advanced.md) **[FR]**

# En cours d'√©criture
### Ce fichier peut contenir des erreurs ou √™tre incomplet.

### Table des mati√®res
- [R√©f√©rences et sources](#r√©f√©rences-et-sources)
- [Introduction](#introduction)
- [√âquilibrage des t√¢ches](#√©quilibrage-des-t√¢ches)


# Introduction

Ce document est une collection de m√©thodes et techniques avanc√©es pouvant √™tre utilis√©es pour am√©liorer les performances de votre datapack. Ceci n'a pas √©t√© r√©alis√© pour √™tre un guide complet, mais plut√¥t une collection de conseils et d'astuces qui peuvent √™tre utilis√©es pour am√©liorer vos datapacks.

Contrairement au document sur [les m√©thodes basiques d'optimisation](./basics.md), ce document n'a pas √©t√© con√ßu pour √™tre lu de haut en bas. Chaque section est ind√©pendante des autres, et vous pouvez les lire dans l'ordre dans lequel vous voulez. C'est pourquoi il n'y a pas de marquage pour chaque section.

Le but est d'archiver des m√©thodes plus avanc√©es, avec des liens vers les sources. Les m√©thodes peuvent √™tre sp√©cifiques au language mcfunction, ou des m√©thodes d'optimisation de programme plus g√©n√©rales.

# √âquilibrage des t√¢ches

L'√©quilibrage des t√¢ches est une optimisation extr√™mement importante pour √©viter les lags, surtout quand il s'agit de grosses op√©rations.

En premier, soulignons une chose : Dans Minecraft, le jeu est mis √† jour (MAJ) 20 fois par secondes. Chaque MAJ est appel√©e un tick. Pour que le jeu fonctionne cette vitesse de rafra√Æchissement, chque tick doit √™tre ex√©cut√© dans un d√©lai de 50 millisecondes maximum.

Chaque commande que l'on ajoute √† notre datapack prend un certain temps pour s'ex√©cuter, et nous avons besoin d'√™tre s√ªrs que chaque tick n'exc√®de pas ce d√©lai. _Bien s√ªr, la quantit√© que chaque ordinateur peut calculer en un tick d√©pend de ce dernier, mais l'id√©e est la m√™me._

Pour d√©montrer ce probl√®me, j'ai fait un datapack qui br√ªle chaque entit√© expos√©es au ciel. Chaque tick, il teste pour chaque entit√© dans le jeu ; s'il est expos√© au ciel, et s'il l'est, il change le NBT `Fire` pour enflammer l'entit√©. Dans notre exemple, la fonction de tester si l'entit√© est expos√©e au ciel est tr√®s gourmande en ressources.

### Le probl√®me

Jetons un oeil au graphique de dur√©e par TPS (que vous pouvez ouvrir en appuyant sur Alt+F3) dans un monde local (solo).

![Normal TPS](../../../assets/load_balancing/graph1.png)

√áa √† l'air bon ! Maintenant, activons notre datapack et allons regarder √† nouveau le graphique.

![Very bad TPS](../../../assets/load_balancing/graph2.png)

Le vert c'est avant d'activer le datapack, le jaune apr√®s. C'est un mauvais signe. Sur certains ordinateurs ou serveurs, cette ligne peut aller dans le rouge et causer le serveur √† fonctionner au ralenti. Cela peut aussi aller dans le rouge en fonction de la croissance d'un datapack.

Nous devons faire en sorte que le graphique soit le plus proche de celui o√π le datapack est d√©sactiv√©.

### Trouver le probl√®me

Nous avons besoin de comprendre si c'est possible d'optimiser ceci. Dans cet exemple, nous savons au pr√©alable que la fonction qui d√©tecte l'exposition des entit√©s au ciel est le probl√®me, elle ne peut pas √™tre plus optimis√©e, puisqu'elle l'est d√©j√†.

En revanche, il y a quand m√™me un moyen. Quand une optimisation directe n'est plus possible, demandez-vous : est-ce que cette fonction √† vraiment besoin d'√™tre ex√©cut√©e tous les ticks ?

Dans notre cas, il n'est pas n√©cessaire que ce soit instantan√©. Quelques ticks de d√©lai ne devrait pas √™tre d√©rangeant.

**Bon... commen√ßons avec les optimisations.**

### Essayer de r√©soudre le probl√®me

En premier, faisons ce la majorit√© des personnes font pour essayer de le r√©soudre : Utiliser `/schedule`. Plut√¥t que l'ex√©cuter chaque tick, on peut la param√©trer pour s'ex√©cuter tous les 5 ticks.

Changeons notre commande en la d√©pla√ßant de la fonction tick (qui s'ex√©cute tous les ticks) √† une fonction sp√©cifique, puis en utilisant `/schedule` pour l'ex√©cuter.

```mcfunction
#> schedule_5t.mcfunction
execute as @e[type=!player] run function example:try_sky_burn

schedule function example:schedule _5t 5t replace
```

```mcfunction
#> on_load.mcfunction
function example:schedule_5t
```

C'est d√©j√† assez d'ex√©cuter la finction tous les 5 ticks.

Rechargeons le datapack et regardons √† nouveau le graphique.

![Lag Spikes](../../../assets/load_balancing/graph3.png)

Diriez-vous que c'est acceptable ? Non ! Peut-√™tre que c'est bien mieux, mais regardez ces pics ! La diff√©rence entre avant et maintenant est qu'avant tous les ticks √©taient lents, maintenant juste certains. Cela peut aussi causer des lags !

Il nous faut une m√©thode pour √©liminer ces pics, mais... comment ?

### La bonne solution pour ce probl√®me

Nous devons distribuer le poids de chaque pic sur plusieurs ticks, pour rendre le graphique plat et r√©duire le d√©lai max.

Plut√¥t que d'essayer de tester toutes les entit√©s en un tick tous les 5 ticks, il nous faut tester 1/5 des entit√©s chaque tick, donc tester uniform√©ment une quantit√© pr√©d√©finie d'entit√©s chaque tick.

Pour impl√©menter ce syst√®me, il nous faut s√©parer les entit√©s en 5 groupes. Pour faire ceci, il faut cr√©er un tableau qui va tourner en rond entre 0 et 4 et va progresser avec chaque entit√© ajout√©e au groupe, et va donner un tag (√©tiquette) correspondant au groupe de l'entit√©. _J'utilise un tag plut√¥t qu'un score puisque bien que la v√©rification d'un tag est plus rapide, Minecraft a un bug avec les scores d'entit√©s disparaissant, pouvant aller jusqu'√† la corruption du monde avec le temps._ 

En premier, ajoutons le tableau pour donner un groupe.

```mcfunction
#> on_load.mcfunction
scoreboard objectives add sky_burn_entity_group dummy
scoreboard players set .current_group sky_burn_entity_group 0
scoreboard players set .current_group_to_assign sky_burn_entity_group 0
scoreboard players set .group_count sky_burn_entity_group 5
```

Ensuite, cr√©ons une fonction pour assigner un tag de groupe √† une entit√©.

```mcfunction
#> assign_group.mcfunction
# Cycler les groupes
scoreboard players add .current_group_to_assign sky_burn_entity_group 1
scoreboard players operation .current_group_to_assign sky_burn_entity_group %= .group_count sky_burn_entity_group

# Donner un tag de groupe
execute if score .current_group_to_assign sky_burn_entity_group matches 0 run tag @s add sky_burn_group_0
execute if score .current_group_to_assign sky_burn_entity_group matches 1 run tag @s add sky_burn_group_1
execute if score .current_group_to_assign sky_burn_entity_group matches 2 run tag @s add sky_burn_group_2
execute if score .current_group_to_assign sky_burn_entity_group matches 3 run tag @s add sky_burn_group_3
execute if score .current_group_to_assign sky_burn_entity_group matches 4 run tag @s add sky_burn_group_4

# Donner un autre tag pour dire que l'entit√© √† bien un groupe
tag @s add sky_burn_has_group
```

Maintenant, mettons dans la fonction tick un groupe pour les entit√©s n'ayant pas encore de groupe

```mcfunction
#> on_tick.mcfunction
execute as @e[tag=!sky_burn_has_group] run function example:assign_group
```

C'est d√©j√† suffisant pour donner chaque entit√© un groupe. Notez que j'ai aussi retir√© le `/schedule`, puisqu'on ne l'utilise plus. J'ai aussi supprim√© la fonction `schedule_5t.mcfunction`.

Enfin, il faut modifier la fonction tick pour s√©lectionner un des groupes √† mettre √† jour.

```mcfunction
#> on_tick.mcfunction
# Assigner un groupe aux entit√©s
execute as @e[tag=!sky_burn_has_group] run function example:assign_group

# Cycler les groupes
scoreboard players add .current_group sky_burn_entity_group 1
scoreboard players operation .current_group sky_burn_entity_group %= .group_count sky_burn_entity_group

# Executer la v√©rification de l'entit√© de ce groupe
execute if score .current_group sky_burn_entity_group matches 0 as @e[tag=sky_burn_group_0] run function example:try_sky_burn
execute if score .current_group sky_burn_entity_group matches 1 as @e[tag=sky_burn_group_1] run function example:try_sky_burn
execute if score .current_group sky_burn_entity_group matches 2 as @e[tag=sky_burn_group_2] run function example:try_sky_burn
execute if score .current_group sky_burn_entity_group matches 3 as @e[tag=sky_burn_group_3] run function example:try_sky_burn
execute if score .current_group sky_burn_entity_group matches 4 as @e[tag=sky_burn_group_4] run function example:try_sky_burn
```

Maintenant, rechargeons √† nouveau le datapack et regardons le graphique... et si tout fonctionne, bien s√ªr.

![TPS With appropriate load balancing](../../../assets/load_balancing/graph4.png)

Bien mieux, et tout fonctionne ! Chaque entit√© individuelle est test√©e tous les 5 ticks, mais pas toutes en m√™me temps.

Notez que les pics dans ce graphique viennent du jeu et ce n'est pas la faute de notre code. 

Nous avons √©quilibr√© les t√¢ches en les distribuant sur plusieurs ticks ! Super ü•≥ü•≥

_Les utilisateurs plus attentifs et exp√©riment√©s vont r√©aliser que tester si une entit√© est expos√©e au ciel est quelque chose de trivial utilisant `positioned over`. J'ai ajout√© la ligne `execute as @e as @e as @e` √† la fin de la fonction uniquement pour la rendre gourmande et pour pouvoir d√©montrer ce principe ;)_

### Quand ne pas √©quilibrer les t√¢ches

Vous avez probablement remarqu√© bien que nous avons appliqu√© l'√©quilibrage des t√¢ches correctement, notre datapack poss√®de une marge bien plus consid√©rable puisqu'il ex√©cute des commandes bas√©es sur des groupes. Dans ce cas, c'√©tait une bonne id√©e, en faisant fonctionner le datapack de mani√®re plus efficace. Mais ce n'est pas toujours le cas.

Si vous avez appliqu√© le principe d'√©quilibrage des t√¢ches correctement et que le MSPT (milliseconds per tick) et quand m√™me plus important que sans, c'est que les tests sont plus lourds que les op√©rations et il faut donc ne pas appliquer ce principe. En d'autres mots, s'il n'y a pas de gain significatif de performances, il vaut mieux ne pas l'utiliser.

### Une autre mani√®re de r√©soudre (mais est inconsistant)

Une autre mani√®re de r√©soudre ce probl√®me est d'utiliser un predicate al√©atoire et de s√©lectionner les entit√©s bas√© l√†-dessus, par exemple :

```mcfunction
#> Fonction ex√©cut√©e en temps qu'entit√©
execute if predicate example:20_percent_chance run function example:random_entity_check
```

Math√©matiquement parlant, 20% de chance par entit√© √©quivaut √† diviser 1/5 des entit√©s par tour, avec beaucoup de simplicit√© non ?

Plut√¥t... Le probl√®me √©tant **l'inconsistance**. M√™me si en moyenne cela sera ex√©cut√© sur 1/5 des entit√©s, ce ne sera pas toujours le cas. Il peut y avoir des ticks o√π beaucoup d'entit√©s sont test√©es en m√™me temps, et il y aurait donc des ticks avec quasiment aucune entit√© test√©e ! Cette commande peut causer des **pics de lag al√©atoires** !

Dans cet exemple, je mets 5 ticks de d√©lai entre chaque entit√©, mais dans des conditions r√©elles, je souhaite avoir un d√©lai pr√©cis de 20 ticks, pour pouvoir m'aligner avec les d√©g√¢ts de feu d'une seconde et causer 1 d√©g√¢t par seconde. Donc, dans ce sc√©nario, je ne peux pas avoir de l'al√©atoire.

Mais, gr√¢ce √† sa simplicit√©, il y a une possibilit√© d'optimisation suppl√©mentaire. Je pr√©f√®re les solutions me donnant un contr√¥le sur ce qui ce passe plut√¥t qu'une fonction al√©atoire, √† cause des inconsistances. Il faut garder √† l'esprit que nous voulons un graphique le plus plat possible, sans vagues et pics. En tant que d√©veloppeur, c'est √† vous d'analyser et de voir ce qui vaut la peine d'utiliser dans votre cas.

### Conclusion

Nous devons utiliser le principe d'√©quilibrage des t√¢ches pour distribuer le poids de nos commandes sur de multiples ticks.

Sachant cela, gardez √† l'esprit que ceci n'est pas la seule m√©thode pour √©quilibrer vos t√¢ches. Vous pouvez vouloir √©taler vos t√¢ches d'une autre fa√ßon, par example en modifiant de larges zones de blocs en zones plus petites sur plusieurs ticks. Vous pouvez aussi vouloir avoir 2 calendriers (schedules) qui s'ex√©cutent tous les 2 ticks, intercal√©s avec chaque faisant une t√¢che compl√®tement diff√©rente ou entrelac√©e. L'importance et de distribuer le poids de vos commandes sur de multiples ticks pour garder un graphique plat et √©viter une surcharge des ticks.

# R√©f√©rences et sources

[‚Üê Retour au README](../../../README.md)
